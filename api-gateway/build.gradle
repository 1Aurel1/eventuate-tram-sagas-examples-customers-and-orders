plugins {
    id "de.undercouch.download" version "4.0.0"
}

apply plugin: "io.spring.dependency-management"

apply plugin: "java"
apply plugin: 'org.springframework.boot'

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-gateway:$springCloudGatewayVersion"
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:$springCloudContractDependenciesVersion"
        mavenBom "org.springframework.cloud:spring-cloud-sleuth:${project.ext.springCloudSleuthVersion}"
    }
}

configurations.all {
    exclude  module: "spring-boot-starter-web"
}

task swaggerUiDownload {
    doLast {
        download {
            src "https://github.com/swagger-api/swagger-ui/archive/v${swaggerUiVersion}.zip"
            dest "${buildDir}/swagger-ui.zip"
            onlyIfNewer true
        }
    }
}

task unzipSwaggerUi(type: Copy) {
    dependsOn swaggerUiDownload
    from zipTree(file("${buildDir}/swagger-ui.zip"))
    include "*/dist/**"
    into file("${buildDir}")
}

bootJar {
    dependsOn unzipSwaggerUi

    from("${buildDir}/swagger-ui-${swaggerUiVersion}/dist") {
        into "BOOT-INF/classes/swagger-ui"
        exclude "index.html"
    }

    from("${buildDir}/swagger-ui-${swaggerUiVersion}/dist/index.html") {
        into "BOOT-INF/classes/swagger-ui"
        filter {it.replaceAll("\"https://petstore.swagger.io/v2/swagger.json\"", "window.location.origin + \"/swagger-ui/swagger.yml\"") }
    }
}

dependencies {
    compile "commons-lang:commons-lang:2.6"

    compile project(":api-gateway-web-api")

    compile "org.springframework.boot:spring-boot-starter-webflux"
    compile "org.springframework.cloud:spring-cloud-starter-gateway:$springCloudGatewayVersion"
    compile "org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j:1.0.2.RELEASE"
    compile "io.github.resilience4j:resilience4j-spring-boot2:1.1.0"
    compile "org.apache.commons:commons-lang3:3.6"
    compile "org.springframework.boot:spring-boot-starter-actuator"

    compile 'org.springframework.cloud:spring-cloud-starter-sleuth'
    compile 'org.springframework.cloud:spring-cloud-starter-zipkin'
    compile 'io.zipkin.brave:brave-bom:4.17.1'

    testCompile "org.springframework.cloud:spring-cloud-contract-wiremock"
    testCompile "junit:junit:4.12"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}
